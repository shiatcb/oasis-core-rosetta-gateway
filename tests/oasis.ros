// Portions of this are modeled after Rosetta's Ethereum transfer test, which
// also uses an accounts based model.
// https://github.com/coinbase/rosetta-ethereum/blob/master/rosetta-cli-conf/testnet/ethereum.ros

create_account(1){
	create{
		network_list = load_env("OASIS_ROSETTA_NETWORK_LIST");
		network = {{network_list.network_identifiers.0}};
		key = generate_key({"curve_type": "edwards25519"});
		account = derive({
			"network_identifier": {{network}},
			"public_key": {{key.public_key}}
		});

		save_account({
			"account_identifier": {{account.account_identifier}},
			"keypair": {{key}}
		});
	}
}

transfer(10){
	transfer{
		network_list = load_env("OASIS_ROSETTA_NETWORK_LIST");
		transfer.network = {{network_list.network_identifiers.0}};
		fee_accumulator = {
			"address": "oasis1qqnv3peudzvekhulf8v3ht29z4cthkhy7gkxmph5"
		};
		currency = {"symbol": "ROSE", "decimals": 9};
		sender = find_balance({
			"minimum_balance": {"value": "101", "currency": {{currency}}}
		});

		amount_fee = "100";
		sender_amount_fee = 0 - {{amount_fee}};
		available_amount = {{sender.balance.value}} - {{amount_fee}};
		recipient_amount = random_number({"minimum": "1", "maximum": {{available_amount}}});
		print_message({"recipient_amount": {{recipient_amount}}});

		sender_amount = 0 - {{recipient_amount}};
		recipient = find_balance({
			"not_account_identifier": [{{sender.account_identifier}}],
			"minimum_balance": {"value": "0", "currency": {{currency}}},
			"create_limit": 100,
			"create_probability": 50
		});
		transfer.confirmation_depth = "1";
		transfer.operations = [
			{
				"operation_identifier": {"index": 0},
				"type": "Transfer",
				"account": {{sender.account_identifier}},
				"amount": {"value": {{sender_amount_fee}}, "currency": {{currency}}}
			},
			{
				"operation_identifier": {"index": 1},
				"type": "Transfer",
				"account": {{fee_accumulator}},
				"amount": {"value": {{amount_fee}}, "currency": {{currency}}}
			},
			{
				"operation_identifier": {"index": 2},
				"type": "Transfer",
				"account": {{sender.account_identifier}},
				"amount": {"value": {{sender_amount}}, "currency": {{currency}}}
			},
			{
				"operation_identifier": {"index": 3},
				"type": "Transfer",
				"account": {{recipient.account_identifier}},
				"amount": {"value": {{recipient_amount}}, "currency": {{currency}}}
			}
		];
	}
}
